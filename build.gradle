import java.time.LocalDateTime

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'maven-publish'
    id 'java-library'
}

group = 'net.bobolabs'
version = '0.0.2'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    compileOnly 'org.jetbrains:annotations:24.0.1'                  // JetBrains annotations

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = 'BoboLabs'
            url = 'https://repo.bobolabs.net/repository/maven-releases/'
            credentials {
                username = project.property('bobolabs.user')
                password = project.property('bobolabs.key')
            }
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            groupId = project.group
            artifactId = project.name
            version = project.version

            project.shadow.component(publication)
        }
    }
}

shadowJar {
    archiveBaseName.set(project.name)
    archiveClassifier.set('')

    from "${project.rootDir}/LICENSE.txt"
}

license {
    header = rootProject.file('HEADER.txt')
    include '**/*.java'

    properties {
        year = LocalDateTime.now().getYear()
        projectName = project.name
    }
}

tasks.jar.enabled = false
tasks.build.dependsOn(shadowJar)